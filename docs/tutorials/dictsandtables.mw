= More about tables =

== Keyed and unkeyed tables ==

In Kdb+, an unkeyed table is a list of dictionaries.

To illustrate this, we can create a table out of a dictionary.

<pre>
q)dict
sym   | `ibm
volume| 100
q)enlist dict
sym volume
----------
ibm 100
</pre>

If we replicate the single element of that list, we get a table with copies of the same row:

<pre>
q)3 # enlist dict
sym volume
----------
ibm 100
ibm 100
ibm 100
</pre>

A keyed table is not a list. It's a dictionary. It maps each row in a table of unique keys to a corresponding row in a table of values.

<pre>
q)kt
sym| volume
---| ------
ibm| 100
amd| 200
</pre>

The key in the dictionary is a table

<pre>
q)key kt
sym
---
ibm
amd
</pre>

and so is the value stored in the dictionary

<pre>
q)value kt
volume
------
100
200
</pre>

This difference in representation affects the operations that are
supported by each kind of table. We can illustrate the differences with an example. Let's define two keyed tables and two unkeyed ones with the same contents:

<pre>
q)kt:([sym:`ibm`amd] volume:100 200)
q)ku:([sym:`amd`intel] volume:300 400)

q)t:([]sym:`ibm`amd; volume:100 200)
q)u:([]sym:`amd`intel; volume:300 400)
</pre>

Positional indexing works on unkeyed tables, but not on keyed ones.

<pre>
q)t[0]
sym   | `ibm
volume| 100
q)kt[0]
'type
</pre>

On the other hand, we can index keyed tables by key:

<pre>
q)t[`ibm]
`symbol$()
q)kt[`ibm]
volume| 100
q)kt[`notpresent]
volume|
</pre>

The result of the <tt>,</tt> operator depends on the type of table arguments:

<pre>
q)t,u
sym   volume
------------
ibm   100
amd   200
amd   300
intel 400
q)kt,ku
sym  | volume
-----| ------
ibm  | 100
amd  | 300
intel| 400
q)kt,u
'type
</pre>

And the same is true for table arithmetic:

<pre>
q)t+u
'type
q)kt+ku
sym  | volume
-----| ------
ibm  | 100
amd  | 500
intel| 400
q)kt+u
sym| volume sym
---| ------------
ibm| 400    amd
amd| 600    intel
</pre>

Finally, let's check the result of <tt>^</tt>:

<pre>
q)kr:([sym:`ibm`amd]; price:10 20)
q)kt^kr
sym| volume price
---| ------------
ibm| 100    10
amd| 200    20
q)t^kr
sym| sym volume price
---| ----------------
ibm| ibm 100    10
amd| amd 200    20
</pre>
